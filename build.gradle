plugins {
    //support Idea IDE
    id 'idea'
    id "io.spring.dependency-management" version "1.0.10.RELEASE"
    // Build uber-jar
    id 'com.github.johnrengelman.shadow' version '5.2.0' apply false
}

idea {
    project {
        languageLevel = 13
    }
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

allprojects {
    group "ru.otus"

    repositories {
        mavenCentral()
    }

    apply plugin: "io.spring.dependency-management"
    dependencyManagement {
        dependencies {
            imports {
                mavenBom("org.springframework.boot:spring-boot-dependencies:2.3.3.RELEASE")
            }
            dependency("org.springframework.boot:spring-boot-starter-web:2.3.3")
            dependency("org.springframework.boot:spring-boot-starter-test:2.3.3")
            dependency("com.google.guava:guava:${Versions.guava}")
            dependency("org.openjdk.jol:jol-core:${Versions.jol}")
            dependency("org.ow2.asm:asm-commons:${Versions.asm}")
            dependency("org.glassfish:javax.json:${Versions.glassfishJson}")
            dependency("javax.xml.bind:jaxb-api:${Versions.jaxbApi}")
            dependency("org.projectlombok:lombok:${Versions.lombok}")
        }
    }

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()
        }

        resolutionStrategy {
            force("javax.servlet:servlet-api:2.4")
            force("commons-logging:commons-logging:1.1.1")
            force("commons-lang:commons-lang:2.5")
            force("org.codehaus.jackson:jackson-core-asl:1.8.8")
            force("org.codehaus.jackson:jackson-mapper-asl:1.8.3")
            force("org.codehaus.jettison:jettison:1.1")
            force("org.javassist:javassist:3.24.0-GA")
            force("org.apiguardian:apiguardian-api:1.1.0")
            force("org.opentest4j:opentest4j:1.2.0")
        }
    }
}

//print versions from BOM
task managedVersions {
    doLast {
        dependencyManagement.managedVersions.each {
            println it
        }
    }
}